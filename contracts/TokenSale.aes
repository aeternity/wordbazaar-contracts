// ISC License
//
// Copyright (c) 2020, aeternity developers
//
// Permission to use, copy, modify, and/or distribute this software for any
// purpose with or without fee is hereby granted, provided that the above
// copyright notice and this permission notice appear in all copies.
//
// THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
// REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY
// AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
// INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
// LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE
// OR OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
// PERFORMANCE OF THIS SOFTWARE.


// THIS IS NOT SECURITY AUDITED
// DO NEVER USE THIS WITHOUT SECURITY AUDIT FIRST

contract Token =
  stateful entrypoint mint : (address, int) => unit
  stateful entrypoint transfer_allowance : (address, address, int) => unit
  stateful entrypoint burn : (int) => unit
  stateful entrypoint total_supply : () => int

contract Vote =
  type vote_option = bool
  type amount = int
  type vote_state = map(vote_option, amount)

  entrypoint token : () => Token
  entrypoint final_vote_state : () => option(vote_state)
  entrypoint subject_workaround : () => (string * address)
  entrypoint close_height : () => int

contract BondingCurve =
  entrypoint buy_price : (int) => int
  entrypoint sell_price : (int) => int
  entrypoint calculate_buy_price : (int, int) => int
  entrypoint calculate_sell_return : (int, int) => int

contract TokenSale =

  record state =
    { token : option(Token)
    , votes : map(int, (bool * Vote))
    , spread : int
    , vote_timeout : int
    , bonding_curve : BondingCurve
    , description : string }

  datatype event =
    AddVote(Vote, int)

  stateful entrypoint init(vote_timeout : int, bonding_curve : BondingCurve, description : string) : state =
    // TODO uncomment for correct timeout check, or remove timeout parameter
    // require(vote_timeout == (60 / 3) * 24 * 7, "INCORRECT_VOTE_TIMEOUT")
    { token = None, votes = {}, spread = 0, vote_timeout = vote_timeout, bonding_curve = bonding_curve, description = description }

  stateful entrypoint set_token(token : Token) : unit = put(state{ token = Some(token) })

  entrypoint calculate_buy_price(amount : int) : int =
    state.bonding_curve.calculate_buy_price(get_token().total_supply(), amount)

  payable stateful entrypoint buy(amount : int) : unit =
    let price = calculate_buy_price(amount)
    require(Call.value == price, "AMOUNT_NOT_EQUAL")
    get_token().mint(Call.caller, amount)
    let reserve = state.bonding_curve.calculate_sell_return(get_token().total_supply(), get_token().total_supply())
    put(state{ spread = (Contract.balance - reserve) })

  entrypoint calculate_sell_return(amount : int) : int =
    state.bonding_curve.calculate_sell_return(get_token().total_supply(), amount)

  stateful entrypoint sell(amount : int) : unit =
    get_token().transfer_allowance(Call.caller, Contract.address, amount)
    let return = calculate_sell_return(amount)
    get_token().burn(amount)
    Chain.spend(Call.caller, return)

  stateful entrypoint add_vote(vote : Vote) =
    require(vote.token() == get_token(), "NOT_SAME_TOKEN_AS_SALE")
    let vote_seq_id = Map.size(state.votes)
    put(state{ votes[vote_seq_id] = (false, vote) })
    Chain.event(AddVote(vote, vote_seq_id))
    vote_seq_id

  stateful entrypoint apply_vote_subject(vote_seq_id : int) =
    let (applied, vote) = state.votes[vote_seq_id]
    require(!applied, "VOTE_ALREADY_APPLIED")
    require(vote.close_height() + state.vote_timeout >= Chain.block_height, "VOTE_RESULT_TIMEOUTED")
    let apply_subject = () =>
      let (subject, receiver) = vote.subject_workaround()
      if(subject == "VotePayout")
        Chain.spend(receiver, state.spread)
        put(state{ spread = 0, votes[vote_seq_id] = (true, vote) })

    switch(vote.final_vote_state())
      None => abort("VOTE_NOT_YET_FINAL")
      Some(vote_state) =>
        require(vote_state[true] > vote_state[false], "GREATER_50_PERCENT_AGREEMENT_REQUIRED")
        require(vote_state[true] > get_token().total_supply() / 2, "GREATER_50_PERCENT_STAKE_REQUIRED")
        apply_subject()

  entrypoint prices() : (int * int) =
    (state.bonding_curve.buy_price(get_token().total_supply()), state.bonding_curve.sell_price(get_token().total_supply()))

  entrypoint votes() : map(int, (bool * Vote)) = state.votes
  entrypoint spread() : int = state.spread
  entrypoint vote_timeout() : int = state.vote_timeout
  entrypoint get_state() : state = state

  entrypoint get_token() : Token =
    switch(state.token)
      Some(token) => token
      None => abort("NO_TOKEN_SET")
